#####################
# Transition screens and animations

: death-by-faraway-snake-animation
  # Animate snake attack abusing render-3d routines

  # Frame 0
  i := long sprites+32+0
  foreground-draw
  i := long sprites+16+16
  foreground-draw

  play-click
  v1 := 20
  wait

  return

: death-by-snake-animation
  # Animate snake attack abusing render-3d routines

  # Frame 1
  i := long sprites+16+16
  foreground-draw
  i := long sprites+16+0
  foreground-draw

  play-click
  v1 := 20
  wait

  # Frame 2
  clear
  i := long viper4+16+0
  v0 := 16
  render-column
  i := long viper4+32+0
  v0 := 32
  render-column
  column-one
  column-four

  # "Death"
  play-click
  v1 := 30
  wait
  play-click
  scroll-up-animation
  v1 := 50
  wait
  clear
  v1 := 30
  wait
  return

: snakeless-death-animation
  play-click
  v1 := 30
  wait
  play-click
  v0 := 0
  loop
    scroll-up 1
    v1 := 10
    wait
    v0 += 1
    if v0 != 10 then
  again
  v1 := 50
  wait
  clear
  v1 := 30
  wait
  return

: scroll-up-animation
  v0 := 0
  loop
    scroll-up 1
    i := long viper-neck+16+0
    v1 := 16
    v2 := 16
    sprite v1 v2 0
    i := long viper-neck+32+0
    v1 += 16
    sprite v1 v2 0
    v1 := 10
    wait
    scroll-up 1
    i := long viper-neck+16+16
    v1 := 16
    v2 := 16
    sprite v1 v2 0
    i := long viper-neck+32+16
    v1 += 16
    sprite v1 v2 0
    v1 := 10
    wait
    v0 += 1
    if v0 != 5 then
  again
  return

: wait
  delay := v1
  loop
    v1 := delay
    if v1 != 0 then
  again
  return

: title-screen
  # Select background music
  unpack-long im-too-young-to-vipe
  select-song
  # Render this image
  i := long title-screen+0+0
  jump default-bitmap-wait

: clown-screen1
  i := long clown+0+0
  jump default-bitmap-wait

: clown-screen2
  i := long clown2+0+0
  jump default-bitmap-wait

: map-victory-screen
  set-text-invisible
  # Select background music
  unpack-long im-too-young-to-vipe
  select-song
  # Select immediate sound effect
  unpack-long victory-1
  interject-song
  # Render this image
  i := long map-victory+0+0
  # Fall through

: default-bitmap-wait
  bitmap-to-screen
  # Blink text
  unpack-long string-press-any-key
  select-text
  set-text-visible
  # And wait for a key press
  wait-key-release
  wait-key-press
  set-text-invisible
  jump wait-key-release

: map-stats-screen
  set-text-invisible
  # Render background
  i := long stats-screen+0+0
  bitmap-to-screen
  # Render stats
  plane 1
  i := long game-stats
  load v2
  v5 := v1 # Save, because get-number messes it up
  # Coins
  get-number
  v3 := 36
  v4 := 7
  sprite v3 v4 5
  map-metadata-start
  v0 := 5
  i += v0
  load v0
  get-number
  v3 := 43
  sprite v3 v4 5
  # Secrets
  v0 := v2
  get-number
  v3 := 11
  v4 := 19
  sprite v3 v4 5
  map-metadata-start
  v0 := 4
  i += v0
  load v0
  get-number
  v3 := 18
  sprite v3 v4 5
  # Deaths
  i := long bcd-pad
  bcd v5
  v3 := 33
  v4 := 1
  i := long bcd-pad
  load v2
  v5 := v1 # Save, because get-number messes it up
  if v0 == 0 then jump map-stats-skip-digit-one
  get-number
  sprite v3 v4 5
: map-stats-skip-digit-one
  v3 += 4
  if v1 == 0 then jump map-stats-skip-digit-two
  v0 := v5
  get-number
  sprite v3 v4 5
: map-stats-skip-digit-two
  v3 += 4
  v0 := v2
  get-number
  sprite v3 v4 5
  plane 3
: map-stats-wait-key
  wait-key-release
  wait-key-press
  if v5 == OCTO_KEY_A then jump map-stats-go-back
  if v5 == OCTO_KEY_D then jump map-stats-next
  jump map-stats-wait-key
: map-stats-next
  map-metadata-start
  v0 := 2
  i += v0
  load v1
  select-map
  i := long game-stats
  v0 := 0
  v1 := 0
  v2 := 0
  save v2
: map-stats-go-back
  send-player-back-to-start
  jump wait-key-release

: bitmap-to-screen
  clear
  v0 := 0
  v2 := 64
: bitmap-to-screen-loop
  v1 := 0
  sprite v0 v1 0
  i += v2
  v1 := 16
  sprite v0 v1 0
  i += v2
  v0 += 16
  if v0 != 64 then jump bitmap-to-screen-loop
  return
