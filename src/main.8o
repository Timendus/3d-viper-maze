#####################
# 3D Viper Maze
# An Octojam 7 entry
# By Timendus
# https://github.com/Timendus/3d-viper-maze

: main
  lores
  plane 3

  # Prepare virtual timers
  music-delay := 1
  text-delay := 1
  v0 := 1
  delay := v0

  # Welcome the user!
  title-screen

  # Start with training map
  :unpack 0 training1
  select-map
  start-map

  unpack-long string-button-pressed
  select-text

: main-redraw
  clear
  render-3d
  mark-screen-clean
: main-loop
  check-keys
  clock-tick
  i := long dirty    # Is the screen dirty?
  load v0
  if v0 == 1 then jump main-redraw
  jump main-loop

# Handle those things that need attention in parallel to gameplay,
# like sound and enemy movement. A virtual timer implementation in the upper
# registers.
: clock-tick
  v0 := delay
  if v0 != 0 then return
  music-delay -= 1
  text-delay -= 1
  if music-delay == 0 then play-music
  if text-delay == 0 then show-text
  v0 := 1
  delay := v0
  return

# Mark the screen dirty or clean to manage redraws

: mark-screen-dirty
  i := long dirty
  v0 := 1
  save v0
  return

: mark-screen-clean
  i := long dirty
  v0 := 0
  save v0
  return
