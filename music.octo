: main
  play-music
  jump main

:const c-note    0
:const c-repeat  1
:const c-da-capo 2

#####################
# A non-blocking music player
# Needs to be called at a reasonable frequency to keep the melody going
# Also, it hogs the buzzer and delay special registers. So if you need to use
# those, stop calling this routine and reset the buzzer to zero.
: play-music
  # Is the last note finished?
  v0 := delay
  if v0 != 0 then return

  # Load and update our position in the song
  i := music-position
  load v0
  v4 := v0
  v0 += 1
  i := music-position
  save v0

  # Load the next music instruction
  i := long knee-deep-in-trouble
  i += v4
  i += v4
  i += v4
  i += v4
  load v3

  if v2 == c-note   then jump play-music-note
  if v2 == c-repeat then jump play-music-repeat
  # Else, it's a da capo

: play-music-da-capo
  v0 := 0
  i := music-position
  save v0
  return

: play-music-note
  i := play-music-smc
  save v1
  0xF0 0x00 # i := long ...
: play-music-smc
  0 0
  # Play the sound!
  audio
  delay := v3
  vf := 255
  buzzer := vf
  return

: play-music-repeat
  i := long knee-deep-in-trouble
  i += v4
  i += v4
  i += v4
  i += v4
  if v0 == 0 begin
    # We've done all the repetitions, reset for the next playthrough
    v0 := v1
    save v0
  else
    # Decrease the loop counter
    v0 -= 1
    save v0

    # Move the position back as many steps as the instruction dictates
    i := music-position
    load v0
    v0 -= v3
    i := music-position
    save v0
  end
  jump play-music

: music-position
  0

#####################
# Musical accompaniment

# Convenience macros

:macro note ADDRESS DURATION {
  :byte { ADDRESS >> 8 }
  :byte { ADDRESS }
  :byte c-note
  :byte { DURATION }
}

:macro rest DURATION {
  :byte { --- >> 8 }
  :byte { --- }
  :byte c-note
  :byte { DURATION }
}

:macro repeat TIMES LABEL {
  :byte { TIMES - 1 }
  :byte { TIMES - 1 }
  :byte c-repeat
  :byte { ( ( HERE - LABEL ) / 4 ) + 1 }
}

:macro da-capo {
  :byte 0
  :byte 0
  :byte c-da-capo
  :byte 0
}

# Available samples

: A$1 0xFF 0xFF 0xFF 0xFF 0xE0 0x00 0x00 0x00 0x07 0xFF 0xFF 0xFF 0xFE 0x00 0x00 0x00
: C-2 0xFF 0xFF 0xFF 0xFE 0x00 0x00 0x00 0x03 0xFF 0xFF 0xFF 0xF0 0x00 0x00 0x00 0x1F
: C$2 0xFF 0xFF 0xFF 0xF8 0x00 0x00 0x00 0x3F 0xFF 0xFF 0xFE 0x00 0x00 0x00 0x0F 0xFF
: D-2 0xFF 0xFF 0xFF 0xF0 0x00 0x00 0x01 0xFF 0xFF 0xFF 0xC0 0x00 0x00 0x07 0xFF 0xFF
: D$2 0xFF 0xFF 0xFF 0xC0 0x00 0x00 0x0F 0xFF 0xFF 0xFC 0x00 0x00 0x01 0xFF 0xFF 0xFF
: E-2 0xFF 0xFF 0xFF 0x80 0x00 0x00 0x7F 0xFF 0xFF 0x80 0x00 0x00 0x3F 0xFF 0xFF 0xC0
: F-2 0xFF 0xFF 0xFE 0x00 0x00 0x03 0xFF 0xFF 0xF8 0x00 0x00 0x0F 0xFF 0xFF 0xE0 0x00
: F$2 0xFF 0xFF 0xFC 0x00 0x00 0x0F 0xFF 0xFF 0x80 0x00 0x01 0xFF 0xFF 0xF8 0x00 0x00
: G-2 0xFF 0xFF 0xF8 0x00 0x00 0x7F 0xFF 0xFC 0x00 0x00 0x3F 0xFF 0xFE 0x00 0x00 0x1F
: A$2 0xFF 0xFF 0xC0 0x00 0x1F 0xFF 0xF0 0x00 0x07 0xFF 0xFC 0x00 0x00 0xFF 0xFF 0x80
: C-3 0xFF 0xFF 0x00 0x01 0xFF 0xFC 0x00 0x03 0xFF 0xF8 0x00 0x0F 0xFF 0xF0 0x00 0x1F


: B-4 0xF8 0x78 0x78 0x78 0x78 0x78 0x78 0x78 0x78 0x78 0x7C 0x3C 0x3C 0x3C 0x3C 0x3C # 494hz
: A$4 0xF8 0x78 0x3C 0x3E 0x1E 0x1F 0x0F 0x07 0x87 0x83 0xC3 0xE1 0xE0 0xF0 0xF0 0x78 # 466hz
: G$4 0xF8 0x3E 0x0F 0x87 0xC1 0xF0 0x7C 0x3E 0x0F 0x83 0xC1 0xF0 0x7C 0x1E 0x0F 0x83 # 415hz
: D$4 0xFE 0x07 0xF0 0x3F 0x81 0xFC 0x0F 0xC0 0x7E 0x03 0xF0 0x1F 0x81 0xFC 0x0F 0xE0 # 311hz
: D-4 0xFE 0x03 0xF8 0x0F 0xE0 0x7F 0x01 0xFC 0x07 0xF0 0x3F 0x80 0xFE 0x03 0xF8 0x1F # 293hz
: C$4 0xFF 0x01 0xFC 0x07 0xF8 0x0F 0xE0 0x3F 0x80 0x7F 0x01 0xFC 0x03 0xF8 0x0F 0xE0 # 277hz
: C-4 0xFF 0x00 0xFE 0x01 0xFE 0x03 0xFC 0x03 0xF8 0x07 0xF0 0x0F 0xF0 0x1F 0xE0 0x1F # 262hz

: --- 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 # silence

: click 0x02 0xCD 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00

# Songs

: knee-deep-in-trouble

: kdt1   note C-2 10  rest 10  note G-2 10  rest 10  note A$1 10  rest 10  note F-2 10  rest 10  repeat 4 kdt1
: kdt2   note C-2 10  rest 10  note D$2 10  rest 10  note G-2 10  rest 10  note C-3 10  rest 10  repeat 2 kdt2
: kdt3   note A$1 10  rest 10  note D-2 10  rest 10  note F-2 10  rest 10  note A$2 10  rest 10  repeat 2 kdt3
: kdt4   note C-2 10  rest 10  note G-2 10  rest 10  note A$1 10  rest 10  note F-2 10  rest 10  repeat 4 kdt4
: kdt5   note C-2 10  rest 10  note C-2 10  rest 10  rest 120                                    repeat 2 kdt5
: kdt6   note C-2 10  rest 10  note G-2 10  rest 10  note A$1 10  rest 10  note F-2 10  rest 10  repeat 4 kdt6
: kdt7   note C-3 10  rest 10  note G-2 10  rest 10  note D$2 10  rest 10  note C-2 10  rest 10  repeat 2 kdt7
: kdt8   note A$2 10  rest 10  note F-2 10  rest 10  note D-2 10  rest 10  note A$1 10  rest 10  repeat 2 kdt8
: kdt9   note C-2 10  rest 10  note G-2 10  rest 10  note A$1 10  rest 10  note F-2 10  rest 10  repeat 4 kdt9
: kdt10  note C-2 10  rest 10  note C-2 10  rest 10  rest 120                                    repeat 2 kdt10
         da-capo

# That's all folks!
